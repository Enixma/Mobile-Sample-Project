apply plugin: 'com.android.application'

apply plugin: 'jacoco'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'realm-android'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.enixma.sample.mobile"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode getBuildVersion() as int
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resValue 'string', 'BASE_URL', 'https://scb-test-mobile.herokuapp.com/api/mobiles/'
        resValue 'string', 'IMAGE_END_POINT', '/images/'
        resValue 'string', 'LOG_REQUEST', 'true'
        resValue 'integer', 'CONNECTION_TIMEOUT_IN_SECOND', '20'
        resValue 'integer', 'READ_TIMEOUT_IN_SECOND', '20'
        resValue 'integer', 'WRITE_TIMEOUT_IN_SECOND', '20'
        resValue 'string', 'SHAREDPREF_FILE', 'app'
        resValue 'string', 'SORTBY_KEY', 'sorting'
        resValue 'string', 'SORTBY_PRICE_LOW_TO_HIGH', 'price_low_to_high'
        resValue 'string', 'SORTBY_PRICE_HIGH_TO_LOW', 'price_high_to_low'
        resValue 'string', 'SORTBY_RATING_FIVE_TO_ONE', 'rating_five_to_one'
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "build", "api"
    productFlavors {
        api16 {
            dimension "api"
        }
        api21 {
            dimension "api"
            minSdkVersion 21
        }
        staging {
            dimension "build"
        }
        prod {
            dimension "build"
        }
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    kapt 'com.android.databinding:compiler:3.0.1'

    // support libs
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'

    // image
    implementation 'com.github.bumptech.glide:glide:3.7.0'

    // acc
    implementation "android.arch.lifecycle:extensions:1.1.0"
    implementation "android.arch.lifecycle:viewmodel:1.1.0"
    kapt "android.arch.lifecycle:compiler:1.1.0"

    // rx
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'
    api 'io.reactivex.rxjava2:rxjava:2.1.6'

    // retrofit
    api 'com.squareup.retrofit2:retrofit:2.3.0'
    api 'com.squareup.retrofit2:converter-gson:2.3.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    api 'com.google.code.gson:gson:2.8.2'

    // dagger
    api 'com.google.dagger:dagger:2.10'
    kapt 'com.google.dagger:dagger-compiler:2.10'

    // test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    testImplementation "org.mockito:mockito-core:2.7.15"

}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    group = "Reporting"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def excludes = ['com/enixma/sample/mobile/data/**/*',
                    'com/enixma/sample/mobile/presentation/**/*',
                    '**/BR.class',
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/databinding/*.class',
                    '**/dagger/*.*',
                    '**/*Injector*.*',
                    '**/*new*.*',
                    '**/*Factory*.*',
                    '**/*ModuleApplication*.*',
                    '**/*Test*.*',
                    'android/**/*.*',
                    '**/*Binding.class',
                    '**/BaseModelTestCase.class',
                    '**/component/*.*',
                    'io/realm/*.class',
                    '**/*Module.*']


    def mainSrc = "${project.projectDir}/src/main/java"
    sourceDirectories = files([mainSrc])

    // generated classes
    classDirectories = fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: excludes
    ) + fileTree(
            dir: "$buildDir/tmp/kotlin-classes/debug",
            excludes: excludes
    )


    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ])
}

static def getBuildVersion() {
    def version = 1
    def buildNumber = System.getenv("CIRCLE_BUILD_NUM")
    if (buildNumber != null) {
        version += buildNumber
    }
    println("VersionCode: " + version)
    return version
}
